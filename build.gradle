ext.scenariooApiVersion = '2.1.0'

/*
 * Gets the version name from the latest Git tag
 * From: http://ryanharter.com/blog/2013/07/30/automatic-versioning-with-git-and-gradle/
 */
def getVersionName = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tags'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

allprojects {
	apply plugin: 'idea'
	apply plugin: 'eclipse'

	group = 'org.scenarioo'
    version = getVersionName()
    tasks.withType(JavaCompile) {
	    sourceCompatibility = "1.6"
	    targetCompatibility = "1.6"
	}
}

// TODO danielsuter extract to separate file
configure([project(":scenarioo-server"), project(":scenarioo-docu-generation-example"), project(":scenarioo-validator")]) {
    apply plugin: 'java'

	repositories {
		maven {
         	url "https://raw.github.com/scenarioo/mvn-repo/master"
        }
	    mavenCentral()
	    mavenLocal()
	}

	dependencies {
		compile 'org.scenarioo:scenarioo-java:' + scenariooApiVersion
		compile 'org.apache.commons:commons-lang3:3.0.1'
		compile 'commons-io:commons-io:2.4'
		compile 'commons-codec:commons-codec:1.2'
		compile 'log4j:log4j:1.2.17'
		testCompile 'junit:junit:4.11'
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.12'
}